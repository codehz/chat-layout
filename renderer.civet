type { RendererOptions, Context, Node, Box, HitTest, DynValue, RenderFeedback } from "./types.civet"
{ shallow, shallowMerge } from "./utils.civet"
{ getNodeParent } from "./registry.civet"

export class BaseRenderer<C < CanvasRenderingContext2D, O < {} = {}>
  graphics: C
  #ctx: Context<C>
  #lastWidth: number
  #cache = new WeakMap<Node<C>, Box>()

  protected get context() shallow #ctx

  @(@graphics: C, readonly options: RendererOptions & O)
    @graphics.textRendering = 'optimizeLegibility'
    self := this
    #ctx = {}
      @graphics
      get remainingWidth() @graphics.canvas.clientWidth
      set remainingWidth(value)
        Object.defineProperty(this, "remainingWidth", value: value, writable: true)
      alignment: "left"
      reverse: false
      measureNode(node: Node<C>) self.measureNode node, this
      @@invalidateNode
      resolveDynValue<T>(value: DynValue<C, T>): T
        if value <? "function"
          value @graphics
        else
          value as T
      with<T>(cb: (g: C) => T): T
        @graphics.save()
        finally @graphics.restore()
        cb @graphics
      splitText(text: string) options.splitText text
    #lastWidth = @graphics.canvas.clientWidth

  invalidateNode(node: Node<C>): void
    #cache.delete node
    it: Node<C>? .= node
    while it = getNodeParent it
      #cache.delete it

  measureNode(node: Node<C>, ctx?: Context<C>): Box
    if #lastWidth != @graphics.canvas.clientWidth
      #cache = new WeakMap<Node<C>, Box>()
      #lastWidth = @graphics.canvas.clientWidth
    else if result? := #cache.get(node)
      return result
    result := node.measure ctx ?? @context
    #cache.set node, result
    result

export class DebugRenderer<C < CanvasRenderingContext2D> < BaseRenderer<C>
  draw(node: Node<C>): boolean
    { clientWidth: viewportWidth, clientHeight: viewportHeight } := @graphics.canvas
    @graphics.clearRect 0, 0, viewportWidth, viewportHeight
    node.draw @context, 0, 0

  hittest(node: Node<C>, test: HitTest): boolean
    node.hittest @context, test

export function memoRenderItem<C < CanvasRenderingContext2D, T < {}>(renderItem: (item: T) => Node<C>)
  cache := new WeakMap<T, Node<C>>()
  function fn(item: T): Node<C>
    return result if result? := cache.get item
    result := renderItem item
    cache.set item, result
    result
  Object.assign fn, reset: cache@delete

export class ListState<T < {}>
  offset = 0
  position = NaN
  items: T[] = []

  unshift(...items: T[]) @unshiftAll items

  unshiftAll(items: T[])
    @position += items#
    @items = items ++ @items

  push(...items: T[]) @pushAll items

  pushAll(items: T[]) @items ++= items

  reset(): void
    @items = []
    @offset = 0
    @position = NaN

  resetScroll(): void
    @offset = 0
    @position = NaN

  applyScroll(delta: number) @offset += delta

DrawItem<C < CanvasRenderingContext2D> ::=
  idx: number
  node: Node<C>
  offset: number
  height: number

export abstract class VirtualizedRenderer<C < CanvasRenderingContext2D, T < {}> < BaseRenderer<C, {
  renderItem: (item: T) => Node<C>
  list: ListState<T>
}>
  get position() @options.list.position
  set position(value) @options.list.position = value

  get offset() @options.list.offset
  set offset(value) @options.list.offset = value

  get items() @options.list.items
  set items(value) @options.list.items = value

  abstract render(feedback?: RenderFeedback): boolean
  abstract hittest(test: HitTest): boolean

  protected _renderDrawList(list: DrawItem<C>[], shift: number, feedback?: RenderFeedback): boolean
    result .= false
    viewportHeight := @graphics.canvas.clientHeight
    for each { idx, node, offset, height } of list
      y := offset + shift
      continue if y + height < 0 || y > viewportHeight
      if feedback?
        feedback.minIdx = isNaN(feedback.minIdx) ? idx : Math.min idx, feedback.minIdx
        feedback.maxIdx = isNaN(feedback.maxIdx) ? idx : Math.max idx, feedback.maxIdx
        if feedback.minIdx === idx
          feedback.min = idx - Math.min(0, y) / height
        if feedback.maxIdx === idx
          feedback.max = idx - Math.max(0, y + height - viewportHeight) / height
      result = true if node.draw @context, 0, y
    result

export class TimelineRenderer<C < CanvasRenderingContext2D, T < {}> < VirtualizedRenderer<C, T>
  render(feedback?: RenderFeedback): boolean
    { clientWidth: viewportWidth, clientHeight: viewportHeight } := @graphics.canvas
    @graphics.clearRect 0, 0, viewportWidth, viewportHeight

    drawlength .= 0

    @position = 0 if isNaN @position

    if @offset > 0
      if @position == 0
        @offset = 0
      else
        for i of [@position - 1..>=0]
          item := @items[i]
          node := item |> @options.renderItem
          { height } := @measureNode node
          @position = i
          @offset -= height
          break if @offset <= 0
        @offset = 0 if @position == 0 && @offset > 0

    y .= @offset
    drawlist: DrawItem<C>[] := []

    for i of [@position..<@items#]
      item := @items[i]
      node := item |> @options.renderItem
      { height } := @measureNode node
      if y + height > 0
        drawlist.push idx: i, node: node, offset: y, height: height
        drawlength += height
      else
        @offset += height
        @position = i + 1
      y += height
      break if y >= viewportHeight

    shift .= 0

    if y < viewportHeight
      if @position == 0 && drawlength < viewportHeight
        shift = -@offset
        @offset = 0
      else
        shift = viewportHeight - y
        y = @offset += shift
        lastidx .= -1
        for i of [@position - 1..>=0]
          item := @items[lastidx = i]
          node := item |> @options.renderItem
          { height } := @measureNode node
          drawlength += height
          y -= height
          drawlist.push idx: i, node: node, offset: y - shift, height: height
          break if y < 0
        if lastidx == 0 && drawlength < viewportHeight
          shift = -drawlist.-1.offset
          @position = 0
          @offset = 0

    @_renderDrawList drawlist, shift, feedback

  hittest(test: HitTest): boolean
    viewportHeight := @graphics.canvas.clientHeight
    y .= @offset
    for i of [@position..<@items#]
      item := @items[i]
      node := item |> @options.renderItem
      { height } := @measureNode node
      if test.y < y + height
        shallowMerge test, y: test.y - y
          |> node.hittest @context, .
          |> return
      y += height
      break if y >= viewportHeight
    false

export class ChatRenderer<C < CanvasRenderingContext2D, T < {}> < VirtualizedRenderer<C, T>
  render(feedback?: RenderFeedback)
    { clientWidth: viewportWidth, clientHeight: viewportHeight } := @graphics.canvas
    @graphics.clearRect 0, 0, viewportWidth, viewportHeight

    drawlength .= 0
    @position = @items# - 1 if isNaN @position

    if @offset < 0
      if @position == @items# - 1
        @offset = 0
      else
        for i of [@position + 1..<@items#]
          item := @items[i]
          node := item |> @options.renderItem
          { height } := @measureNode node
          @position = i
          @offset += height
          break if @offset > 0

    y .= viewportHeight + @offset
    drawlist: DrawItem<C>[] := []

    for i of [@position..>=0]
      item := @items[i]
      node := item |> @options.renderItem
      { height } := @measureNode node
      y -= height
      if y <= viewportHeight
        drawlist.push idx: i, node: node, offset: y, height: height
        drawlength += height
      else
        @offset -= height
        @position = i - 1
      break if y < 0

    shift .= 0

    if y > 0
      shift = -y
      if drawlength < viewportHeight
        y = drawlength
        for i of [@position + 1..<@items#]
          item := @items[i]
          node := item |> @options.renderItem
          { height } := @measureNode node
          drawlist.push idx: i, node: node, offset: y - shift, height: height
          y = drawlength += height
          @position = i
          break if y >= viewportHeight
        if drawlength < viewportHeight
          @offset = 0
        else
          @offset = drawlength - viewportHeight
      else
        @offset = drawlength - viewportHeight

    @_renderDrawList drawlist, shift, feedback

  hittest(test: HitTest): boolean
    viewportHeight := @graphics.canvas.clientHeight

    drawlength .= 0

    heights := for i of [@position..>=0]
      item := @items[i]
      node := item |> @options.renderItem
      { height } := @measureNode node
      drawlength += height
      [node, height] as const

    y .= drawlength < viewportHeight ? drawlength : viewportHeight + @offset

    return false if test.y > y

    for each [node, height] of heights
      y -= height
      if test.y > y
        shallowMerge test, y: test.y - y
          |> node.hittest @context, .
          |> return

    false
